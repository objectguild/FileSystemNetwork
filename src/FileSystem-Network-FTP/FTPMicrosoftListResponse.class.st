"
A FTPMicrosoftListResponse parses FTP LIST Responses usually generated by an FTP Server running on Microsoft Windows.

Based on Ruby code from https://github.com/stateless-systems/net-ftp-list.
"
Class {
	#name : #FTPMicrosoftListResponse,
	#superclass : #FTPListResponse,
	#category : #'FileSystem-Network-FTP'
}

{ #category : #parsing }
FTPMicrosoftListResponse >> assignModificationTime: match [
	modificationTime := DateAndTime
		date: (Date unsafeReadFrom: (match subexpression: 2) readStream)
		time: (Time fromString: (match subexpression: 3))
]

{ #category : #parsing }
FTPMicrosoftListResponse >> assignName: match [
	name := match subexpression: 8
]

{ #category : #parsing }
FTPMicrosoftListResponse >> assignSize: match [
	size := self isDirectory
		ifTrue: [ 0 ]
		ifFalse: [ (match subexpression: 6) asInteger ]
]

{ #category : #parsing }
FTPMicrosoftListResponse >> assignType: match [
	type := (match subexpression: 5) = '<DIR>'
		ifTrue: [ #directory ]
		ifFalse: [ #file ]
]

{ #category : #parsing }
FTPMicrosoftListResponse >> parse: aString [
	| match |
	match := ('^\s*' , '([0-9\-:/]{5,})\s+([0-9\-:]{3,}([aApP][mM])?)\s+' , '((<DIR>)|([0-9]+))\s+' , '(\S.*)' , '\s*$')
		asRegex.
	(match matches: aString)
		ifFalse: [ ^ nil ].
	self assignModificationTime: match.
	self assignType: match.
	self assignSize: match.
	self assignName: match
]
